ratings = LOAD '/user/maria_dev/ml-100k/u.data' AS (userID: int, movieID: int, rating: int, ratingTime: int);

metadata = LOAD '/user/maria_dev/ml-100k/u.item' USING PigStorage('|') AS (movieID:int, movieTitle:chararray, releaseDate:chararray, videoRelease:chararray, imdbLink:chararray);

nameLookup = FOREACH metadata GENERATE movieID, movieTitle, ToUnixTime(ToDate(releaseDate, 'dd-MMM-yyyy')) AS releaseTime;

ratingsByMovie = GROUP ratings BY movieID;

avgRatings = FOREACH ratingsByMovie GENERATE group AS movieID, AVG(ratings.rating) AS avgRating, COUNT(ratings.rating) AS countRating;

twoStarMovies = FILTER avgRatings by avgRating < 2.0;

twoStarsWithData = JOIN twoStarMovies BY movieID, nameLookup BY movieID;

/* add */
finalResults = FOREACH twoStarsWithData GENERATE nameLookup::movieTitle AS movieName, twoStarMovies::avgRating AS avgRating, twoStarMovies::countRating AS countRating;

countMovies = ORDER finalResults BY countRating DESC;

DUMP countMovies;